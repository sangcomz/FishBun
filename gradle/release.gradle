/**
 * Use For Upload
 */
version = gradle.versionName

def siteUrl = 'https://github.com/sangcomz/FishBun'      // Homepage URL of the library
def gitUrl = 'https://github.com/sangcomz/FishBun.git'   // Git repository URL
group = "com.sangcomz"                      // Maven Group ID for the artifact


install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'fishbun'
                description = 'The project aims to provide a album.'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'sangcomz'
                        name 'Jeong SeokWon'
                        email 'sangcomz@naver.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

afterEvaluate {
    javadoc.classpath += project.android.libraryVariants.toList().first().javaCompile.classpath
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir

}
artifacts {
    archives javadocJar
    archives sourcesJar
}

if (project.rootProject.file('local.properties').isFile()) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

// https://github.com/bintray/gradle-bintray-plugin
    bintray {
        user = properties.getProperty("bintray.user", '')
        key = properties.getProperty("bintray.apikey", '')
        publish = true
        configurations = ['archives']
        pkg {
            repo = "maven"
            // it is the name that appears in bintray when logged
            name = "fishbun"
            websiteUrl = siteUrl
            vcsUrl = gitUrl
            licenses = ["Apache-2.0"]
            publish = true
        }
    }


    github {
        owner = 'sangcomz'
        repo = 'Fishbun'
        token = properties.getProperty("github_access_tokens", ' ')
        tagName = version
        targetCommitish = 'master'
        body = """## Release Note
* Refactoring for rapid development:rocket::rocket::rocket:
* Change PickerSpanCount default value (`3`->`4`)
* target SDK 30 support
  * In API 29 or higher, the camera can be used only in the total image folder.
* Fix Issue(#215)
* Change DetailView statusBar color
* Removed Picasso and added Coil
* Support Chinese
* Fix Issue(#223)
"""
        name = version
    }
}

